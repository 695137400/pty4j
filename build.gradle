import java.text.SimpleDateFormat
import org.apache.tools.ant.filters.FixCrLfFilter

plugins {
  id 'java-library'
}

def pathToNativeInJar = 'resources/com/pty4j/native'

version = new File(rootProject.projectDir, 'VERSION').text

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
    compileClasspath += files('lib/purejavacomm.jar')
  }
  test {
    java {
      srcDirs = ['test']
    }
  }
}

compileJava {
  sourceCompatibility = '1.5'
  targetCompatibility = '1.5'
  options.debugOptions.debugLevel = 'lines,vars,source'
}

repositories {
  mavenCentral()
  flatDir dirs: ['lib']
}

jar {
  from('os') {
    include '**/*'
    into pathToNativeInJar
  }
  manifest {
    attributes(
        'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
        'Created-By': "Gradle ${gradle.gradleVersion}",
        'Build-Jdk': "${System.properties['java.runtime.version']}",
        'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    )
  }
}

task sourcesZip(type: Jar, dependsOn: classes) {
  classifier 'src'
  baseName 'pty4j'
  extension 'zip'
  from sourceSets.main.allSource
  filter(FixCrLfFilter.class, eol:FixCrLfFilter.CrLf.newInstance("lf"))
}

artifacts {
  archives sourcesZip
}

task testJar(type: Test, dependsOn: [jar, testClasses]) {
  description = 'Runs tests on built jar instead of build/classes/java/main/**/*.class files'
  group = 'verification'

  testClassesDirs = sourceSets.test.output.classesDirs
  classpath = project.files("$buildDir/libs/" + jar.archiveName,
                            sourceSets.test.output.classesDirs,
                            configurations.testRuntimeClasspath)
  systemProperty 'pty4j.preferred.native.folder', 'false'
  shouldRunAfter test
}

check.dependsOn test, testJar

dependencies {
  implementation ':purejavacomm:'
  implementation 'org.jetbrains:annotations:16.0.2'
  implementation 'com.google.guava:guava:14.0.1'
  implementation "log4j:log4j:1.2.14"
  implementation 'net.java.dev.jna:jna:4.1.0'
  implementation 'net.java.dev.jna:jna-platform:4.1.0'
  testImplementation 'junit:junit:4.12'
}
